import Mathlib.Algebra.Field.Basic
import UnitConjecture.TorsionFree
import UnitConjecture.GroupRing


/-!

## Giles Gardam's result

The proof of the theorem `ğ”½â‚‚[P]` has non-trivial units. Together with the main result of `TorsionFree` -- that `P` is torsion-free, 
this completes the formal proof of Gardam's theorem that Kaplansky's Unit Conjecture is false.
-/


section Preliminaries

/-! ### Preliminaries -/

/-- The definition of an element of a free module being trivial, i.e., of the form `kâ€¢x` for `x : X` and `k â‰  0`. -/
def trivialElem {R X : Type _} [Ring R] [DecidableEq X] (a : FreeModule R X) : Prop :=
  âˆƒ! x : X, FreeModule.coordinates x a â‰  0

/-- The statement of Kaplansky's Unit Conjecture:
The only units in a group ring, when the group is torsion-free and the ring is a field, are the trivial units. -/
def UnitConjecture : Prop :=
  âˆ€ {F : Type _} [Field F] [DecidableEq F] 
  {G : Type _} [Group G] [DecidableEq G] [TorsionFree G],
    âˆ€ u : (F[G])Ë£, trivialElem (u : F[G])

/-- The finite field on two elements. -/
abbrev ğ”½â‚‚ := Fin 2

instance : Field ğ”½â‚‚ where
  inv := id
  exists_pair_ne := âŸ¨0, 1, by decideâŸ©
  mul_inv_cancel := fun
        | 0, h => by contradiction
        | 1, _ => rfl
  inv_zero := rfl
  div_eq_mul_inv := by decide

instance ringElem : Coe P (ğ”½â‚‚[P]) where
    coe g := âŸ¦[(1, g)]âŸ§

end Preliminaries

section Constants

namespace P

/-!
The main constants of the group `P`.
-/

abbrev x : P := (K.x, Q.e)
abbrev y : P := (K.y, Q.e)
abbrev z : P := (K.z, Q.e)
abbrev a : P := ((0, 0, 0), Q.a)
abbrev b : P := ((0, 0, 0), Q.b)

end P

namespace Gardam

open P

/-! The components of the non-trivial unit `Î±`. -/
def p : ğ”½â‚‚[P] := (1 : ğ”½â‚‚[P]) +  x +  y +  x*y +  zâ»Â¹ + x*zâ»Â¹ + y*zâ»Â¹ + x*y*zâ»Â¹
def q : ğ”½â‚‚[P] := (xâ»Â¹*yâ»Â¹ : ğ”½â‚‚[P]) + x + yâ»Â¹*z + z
def r : ğ”½â‚‚[P] := (1 : ğ”½â‚‚[P]) + x + yâ»Â¹*z + x*y*z
def s : ğ”½â‚‚[P] := (1 : ğ”½â‚‚[P]) + x*zâ»Â¹ + xâ»Â¹*zâ»Â¹ + y*zâ»Â¹ + yâ»Â¹*zâ»Â¹

/-- The non-trivial unit `Î±`. -/
def Î± : ğ”½â‚‚[P] := p + (q * a) + (r * b) + (s * a * b)
 
/-! The components of the inverse `Î±'` of the non-trivial unit `Î±`. -/
def p' : ğ”½â‚‚[P] := xâ»Â¹ * (aâ»Â¹  * p * a)
def q' : ğ”½â‚‚[P] := -(xâ»Â¹ * q)
def r' : ğ”½â‚‚[P] := -(yâ»Â¹ * r)
def s' : ğ”½â‚‚[P] := zâ»Â¹ * (aâ»Â¹ * s * a)

/-- The inverse `Î±'` of the non-trivial unit `Î±`. -/
def Î±' : ğ”½â‚‚[P] := p' + (q' * a) + (r' * b) + (s' * a * b)

end Gardam

end Constants


section Verification

/-! 
### Verification

The main verification of Giles Gardam's result. 
-/

namespace Gardam

open P

/-- A proof that `Î±` is non-trivial. -/
theorem Î±_nonTrivial : Â¬ (trivialElem Î±) := by
    intro âŸ¨g, _, (eqg : âˆ€ y, Î±.coordinates y â‰  0 â†’ y = g)âŸ©
    have : zâ»Â¹ = g := by
      apply eqg; native_decide
    have : x * y = g := by
      apply eqg; native_decide
    have : zâ»Â¹ = x * y := by
      refine' Eq.trans _ (Eq.symm _) <;> assumption
    simp at this

/-! The fact that the counter-example `Î±` is in fact a unit of the group ring `ğ”½â‚‚[P]` is verified by 
  computing the product of `Î±` with its inverse `Î±'` and checking that the result is `(1 : ğ”½â‚‚[P])`.

  The computational aspects of the group ring implementation and the Metabelian construction are used here. -/

/-- A proof of the existence of a non-trivial unit in `ğ”½â‚‚[P]`. -/
def Counterexample : {u : (ğ”½â‚‚[P])Ë£ // Â¬(trivialElem u.val)} := 
  âŸ¨âŸ¨Î±, Î±', by native_decide, by native_decideâŸ©, Î±_nonTrivialâŸ©

/-- Giles Gardam's result - Kaplansky's Unit Conjecture is false. -/
theorem GardamTheorem : Â¬ UnitConjecture :=
   fun conjecture => Counterexample.prop <| 
    conjecture (F := ğ”½â‚‚) (G := P) Counterexample.val

end Gardam

end Verification