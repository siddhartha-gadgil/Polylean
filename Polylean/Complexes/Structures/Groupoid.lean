import Polylean.Complexes.Structures.Quiver

/-- A `GroupoidStruct` is a barebones structure for a groupoid containing none of the axioms. -/
class GroupoidStruct (S : Sort _) extends Quiver S where
  id : {X : S} â†’ (X âŸ¶ X)
  comp : {X Y Z : S} â†’ (X âŸ¶ Y) â†’ (Y âŸ¶ Z) â†’ (X âŸ¶ Z)
  inv : {X Y : S} â†’ (X âŸ¶ Y) â†’ (Y âŸ¶ X)

notation "ðŸ™" => GroupoidStruct.id -- type as `\b1`
infixr:80 " â‰« " => GroupoidStruct.comp -- type as `\gg`
infixl:80 " âŠš " => Î» f g => GroupoidStruct.comp g f -- type as `\oo`
postfix:max " â»Â¹ " => GroupoidStruct.inv -- type as `\inv`

/-- A `Groupoid` is defined as a `GroupoidStruct` together with consistency conditions imposed. -/
class Groupoid (S : Sort _) extends GroupoidStruct S where
  id_comp : {X Y : S} â†’ (g : X âŸ¶ Y) â†’ ðŸ™ â‰« g = g
  comp_id : {X Y : S} â†’ (g : X âŸ¶ Y) â†’ g â‰« ðŸ™ = g
  comp_assoc : {W X Y Z : S} â†’ {f : W âŸ¶ X} â†’ {g : X âŸ¶ Y} â†’ {h : Y âŸ¶ Z} â†’ (f â‰« g) â‰« h = f â‰« (g â‰« h)
  inv_comp_id : {X Y : S} â†’ (g : X âŸ¶ Y) â†’ gâ»Â¹ â‰« g = ðŸ™
  comp_inv_id : {X Y : S} â†’ (g : X âŸ¶ Y) â†’ g â‰« gâ»Â¹ = ðŸ™

namespace Groupoid

attribute [simp] id_comp
attribute [simp] comp_id
attribute [simp] comp_assoc
attribute [simp] inv_comp_id
attribute [simp] comp_inv_id

variable {S : Sort _} [G : Groupoid S] {X Y Z : S} (g g' : X âŸ¶ Y) (h h' : Y âŸ¶ Z)

abbrev id' (X : S) : X âŸ¶ X := ðŸ™

@[simp] theorem left_inv_cancel : gâ»Â¹ â‰« (g â‰« h) = h := by
  rw [â† comp_assoc]; simp

@[simp] theorem id_inv : (G.id' X)â»Â¹ = (G.id' X) := by
  have := left_inv_cancel (G.id' X) (G.id' X)
  rw [comp_id, comp_id] at this; assumption

@[simp] theorem inv_inv : (gâ»Â¹)â»Â¹ = g := by
  have := left_inv_cancel (gâ»Â¹) g
  rw [inv_comp_id, comp_id] at this; assumption

@[simp] theorem left_cancel_inv (h : X âŸ¶ Z) : g â‰« (gâ»Â¹ â‰« h) = h := by
  have := left_inv_cancel gâ»Â¹ h
  rw [inv_inv] at this; assumption

@[simp] theorem inv_comp : (g â‰« h)â»Â¹ = hâ»Â¹ â‰« gâ»Â¹ := by
  have := left_cancel_inv (g â‰« h)â»Â¹ (hâ»Â¹ â‰« gâ»Â¹)
  simp at this; assumption

@[simp] theorem left_cancel : g â‰« h = g â‰« h' â†” h = h' :=
  âŸ¨by
    intro hyp
    have := congrArg (gâ»Â¹ â‰« Â·) hyp
    simp at this
    assumption
  , 
    congrArg _âŸ©

@[simp] theorem right_cancel : g â‰« h = g' â‰« h â†” g = g' :=
  âŸ¨by
    intro hyp
    have := congrArg (Â· â‰« hâ»Â¹) hyp
    simp at this
    assumption
  , 
    congrArg (Â· â‰« h)âŸ©

@[simp] theorem left_cancel_id : (g = g â‰« e) â†” ðŸ™ = e := by
  have := left_cancel g ðŸ™ e
  simp at this; assumption

@[simp] theorem left_cancel_id' : (g â‰« e = g) â†” e = ðŸ™ := by
  have := left_cancel g e ðŸ™
  simp at this; assumption
  
@[simp] theorem right_cancel_id : (g = e â‰« g) â†” ðŸ™ = e := by
  have := right_cancel ðŸ™ e g
  simp at this; assumption 

@[simp] theorem right_cancel_id' : (e â‰« g = g) â†” e = ðŸ™ := by
  have := right_cancel e ðŸ™ g
  simp at this; assumption

end Groupoid


/-- A `Functor` is a morphism of `Groupoid`s. -/
structure Groupoid.Functor {S S' : Sort _} (G : Groupoid S) (G' : Groupoid S') 
    extends Quiver.PreFunctor G.toQuiver G'.toQuiver where
  map_comp : {X Y Z : S} â†’ (g : X âŸ¶ Y) â†’ (h : Y âŸ¶ Z) â†’ map (g â‰« h) = map g â‰« map h

namespace Groupoid.Functor

attribute [simp] map_comp

infixr:26 " â¥¤ " => Groupoid.Functor -- type as `\func`

/-- The identity morphism of groupoids. -/
@[simp] protected def id (S : Sort _) [G : Groupoid S] : G â¥¤ G := 
  {obj := id, map := id, map_comp := Î» _ _ => rfl}

instance (S : Sort _) [G : Groupoid S] : Inhabited (G â¥¤ G) := âŸ¨Groupoid.Functor.id SâŸ©

/-- Composition of morphisms of groupoids. -/
@[simp] def comp {S T U : Sort _} {ð”Š : Groupoid S} {â„Œ : Groupoid T} {â„‘ : Groupoid U} 
  (F : ð”Š â¥¤ â„Œ) (G : â„Œ â¥¤ â„‘) : ð”Š â¥¤ â„‘ :=
  {obj := G.obj âˆ˜ F.obj, map := G.map âˆ˜ F.map, map_comp := by simp}

infixr:80 " â‹™ " => comp

variable {R S T : Sort _} [F : Groupoid R] [G : Groupoid S] [H : Groupoid T] 
variable (Î¨ : F â¥¤ G) (Î¦ : G â¥¤ H)

@[simp] theorem map_id {X : S} : Î¦.map (G.id' X) = H.id' (Î¦.obj X) := by
  have := Î¦.map_comp (G.id' X) (G.id' X)
  simp at this
  apply Eq.symm
  assumption

@[simp] theorem map_inv {X Y : S} (g : X âŸ¶ Y) : Î¦.map gâ»Â¹ = (Î¦.map g)â»Â¹ := by
  apply (Groupoid.left_cancel (Î¦.map g) _ _).mp
  rw [â† map_comp]
  simp

@[simp] theorem comp_obj : (Î¦.obj âˆ˜ Î¨.obj) = (Î¨ â‹™ Î¦).obj := rfl

@[simp] theorem comp_obj' : âˆ€ x, (Î¦.obj (Î¨.obj x)) = (Î¨ â‹™ Î¦).obj x := Î» _ => rfl

-- @[simp] theorem comp_map : (Î¦.map âˆ˜ Î¨.map) = (Î¨ â‹™ Î¦).map := sorry

end Groupoid.Functor