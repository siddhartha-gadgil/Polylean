import Mathlib.Algebra.Field.Basic
import Polylean.TorsionFree
import Polylean.GroupRing


/-
The proof of the theorem `ð”½â‚‚[P]` has non-trivial units. Together with the main result of `TorsionFree` -- that `P` is torsion-free, 
this completes the formal proof of Gardam's theorem that Kaplansky's Unit Conjecture is false.
-/


section Preliminaries

/-- definition of being trivial, i.e., of the form `aâ¬g` for `g` a group element and `a â‰  0` -/
def trivialElem {R G : Type _} [Ring R] [DecidableEq R] [Group G] [DecidableEq G] (x : FreeModule R G) : Prop :=
  âˆƒ! g : G, FreeModule.coordinates g x â‰  0

abbrev ð”½â‚‚ := Fin 2

instance : Field ð”½â‚‚ where
  inv := id
  exists_pair_ne := âŸ¨0, 1, by decideâŸ©
  mul_inv_cancel := fun
        | 0, h => by contradiction
        | 1, _ => rfl
  inv_zero := rfl
  div_eq_mul_inv := by decide

instance ringElem: Coe P.P (ð”½â‚‚[P.P]) where
    coe g := âŸ¦[(1, g)]âŸ§

end Preliminaries

section Constants

namespace P

abbrev x : P := (K.x, Q.e)
abbrev y : P := (K.y, Q.e)
abbrev z : P := (K.z, Q.e)
abbrev a : P := ((0, 0, 0), Q.a)
abbrev b : P := ((0, 0, 0), Q.b)

end P

namespace Gardam

open P

/-! The components of the non-trivial unit `Î±`. -/
def p : ð”½â‚‚[P] := (1 : ð”½â‚‚[P]) +  x +  y +  x*y +  zâ»Â¹ + x*zâ»Â¹ + y*zâ»Â¹ + x*y*zâ»Â¹
def q : ð”½â‚‚[P] := (xâ»Â¹*yâ»Â¹ : ð”½â‚‚[P]) + x + yâ»Â¹*z + z
def r : ð”½â‚‚[P] := (1 : ð”½â‚‚[P]) + x + yâ»Â¹*z + x*y*z
def s : ð”½â‚‚[P] := (1 : ð”½â‚‚[P]) + x*zâ»Â¹ + xâ»Â¹*zâ»Â¹ + y*zâ»Â¹ + yâ»Â¹*zâ»Â¹

/-- The non-trivial unit `Î±`. -/
def Î± : ð”½â‚‚[P] := p + (q * a) + (r * b) + (s * a * b)
 
/-! The components of the inverse `Î±'` of the non-trivial unit `Î±`. -/
def p' : ð”½â‚‚[P] := xâ»Â¹ * (aâ»Â¹  * p * a)
def q' : ð”½â‚‚[P] := -(xâ»Â¹ * q)
def r' : ð”½â‚‚[P] := -(yâ»Â¹ * r)
def s' : ð”½â‚‚[P] := zâ»Â¹ * (aâ»Â¹ * s * a)

end Gardam

end Constants


section Verification

namespace Gardam

open P

/-- the inverse `Î±'` of the non-trivial unit `Î±` -/
def Î±' := p' + (q' * a) + (r' * b) + (s' * a * b)

/-- `Î±` is  non-trivial -/
theorem Î±_nonTrivial : Â¬ (trivialElem Î±) := by
    intro contra
    let âŸ¨g, pâŸ© := contra
    let eqg := p.right
    have eqâ‚ : -z = g := by 
      apply eqg
      native_decide
    have eqâ‚‚ : x * y = g := by
      apply eqg
      native_decide
    rw [â† eqâ‚‚] at eqâ‚
    contradiction

/-- The statement of Kaplansky's Unit Conjecture. -/
def UnitConjecture : Prop :=
  âˆ€ {F : Type _} [Field F] [DecidableEq F] 
  {G : Type _} [Group G] [DecidableEq G] [TorsionFree G],
    âˆ€ u : (F[G])Ë£, trivialElem (u : F[G])

/-- The existence of a non-trivial unit in `ð”½â‚‚[P]`. -/
theorem Counterexample : {u : (ð”½â‚‚[P])Ë£ // Â¬(trivialElem u.val)} := 
  âŸ¨âŸ¨Î±, Î±', by native_decide, by native_decideâŸ©, Î±_nonTrivialâŸ©

/-- Giles Gardam's result - Kaplansky's Unit Conjecture is false. -/
theorem Result : Â¬ UnitConjecture :=
   Î» conjecture => Counterexample.prop <| conjecture (F := ð”½â‚‚) (G := P) Counterexample.val

end Gardam

end Verification